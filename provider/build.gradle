plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "au.com.dius.pact" version "4.1.7"
	id "com.wiredforcode.spawn" version "0.8.2"
}

group = 'com.provider'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

task startProvider(type: SpawnProcessTask, dependsOn: 'assemble') {
	command "java -jar ${jar.archivePath}"
	ready 'Started MainApplication'
}

task stopProvider(type: KillProcessTask) {

}

pact {
	serviceProviders {
		'Provider' {
			port = 8181

			startProviderTask = startProvider
			terminateProviderTask = stopProvider

			hasPactWith('Consumer from unit test') {
				file("$buildDir/pacts/Consumer from unit test-Provider.json")
			}
		}
	}
}